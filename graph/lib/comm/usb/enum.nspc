%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%								ENUM.NSPC
%
%		USB device enumeration graph.  Provides dynamic
%		arrival/removal events if supported by underlying
%		system.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

@

	% Context

	% Actions
	$	Initialize			Misc.Dist {}		% Initialize
	$	Uninitialize		Misc.Dist {}		% Uninitialize
	$	Start					Misc.Dist {}		% Start enumerating
	$	Stop					Misc.Dist {}		% Start enumerating

	% Notifications
	$	OnArrive				Misc.Dist {}		% Device arrival
	$	OnRemove				Misc.Dist {}		% Device removal
	$	OnRemoveName		Misc.Dist {}		% Name of device removed
	$	OnEnum				Misc.Dist {}		% Device path enumerating

	% DEBUG
	$	Debug Misc.Debug {}
		!	OnArrive/OnFire						Debug/Fire
		!	OnRemove/OnFire						Debug/Fire
		!	OnRemoveName/OnFire					Debug/Fire
		!	OnEnum/OnFire							Debug/Fire

	%%%%%%%%
	% Setup
	%%%%%%%%

	% MISCN: Create file stream source
	$	CreateFileSrc Misc.Create { Id Io.StmSrcFile }
		!	Initialize/OnFire						CreateFileSrc/Fire

	%%%%%%%%%%
	% Devices
	%%%%%%%%%%

	% MISCN: Create dictionary active devices
	$	CreateDeviceDct Misc.Create { Id Adt.Dictionary }
		!	Initialize/OnFire						CreateDeviceDct/Fire

	%% Arrive

	% ADTN: Store device information in dictionary
	$	StoreDeviceDct Adt.Store {}
		!	CreateDeviceDct/OnFire				StoreDeviceDct/Dictionary
		!	OnArrive/OnFire						StoreDeviceDct/Value

	% ADTN: Load unique name of device
	$	LoadArriveName Adt.Load { Key Name }
		!	OnArrive/OnFire						LoadArriveName/Dictionary
		!	OnArrive/OnFire						LoadArriveName/Fire

	% Store under name
	!	LoadArriveName/OnFire					StoreDeviceDct/Key
	!	LoadArriveName/OnFire					StoreDeviceDct/Fire

	%% Remove

	% ADTN: Load device information from dictionary
	$	LoadRemoveDct Adt.Store {}
		!	CreateDeviceDct/OnFire				LoadRemoveDct/Dictionary
		!	OnRemoveName/OnFire					LoadRemoveDct/Key
		!	OnRemoveName/OnFire					LoadRemoveDct/Fire

	% ADTN: Remove device
	$	RemoveDct Adt.Remove {}
		!	CreateDeviceDct/OnFire				RemoveDct/Container
		!	OnRemoveName/OnFire					RemoveDct/Key
		!	OnRemoveName/OnFire					RemoveDct/Fire

	% Announce
	!	LoadRemoveDct/OnFire						OnRemove/Fire

	%%%%%%%%%%%%
	% Enumerate
	%%%%%%%%%%%%

	%% On startup all openable USB devices are 'arrived' from the system's perspective.
	%% Enumerate and report existing devices.

	% ION: Enumerator (GUID_DEVINTERFACE_USB_DEVICE)
	$	EnumDev Io.EnumDevices { Class "{A5DCBF10-6530-11D2-901F-00C04FB951ED}" }
		!	Start/OnFire							EnumDev/First
%		!	EnumDev/OnNext							Debug/Fire

	% Notify
	!	EnumDev/OnNext								OnEnum/Value
	!	EnumDev/OnNext								OnEnum/Fire

	% Next device
	!	EnumDev/OnNext								EnumDev/Next

	%%%%%%%%%
	% Device
	%%%%%%%%%

	%% TODO: Remove opening device/WinUSB depedency for all USB devices.

	% MISCN: True if device is valid/was successfully opened
	$	DevUsbOpened Misc.Toggle {}
		!	OnEnum/OnFire							DevUsbOpened/False

	%% Device stream

	% ION: Access an asynchronous stream for the device path for querying purposes
	$	DevStmSrc Io.StreamSource { Options { ReadOnly false:bool Async true:bool } }
		!	CreateFileSrc/OnFire					DevStmSrc/Source
		!	OnEnum/OnFire							DevStmSrc/Location

	% USBN: Access the device to retreive vendor Id/device Id
	$	DevUsb Usb.Device {}
		!	DevStmSrc/OnOpen						DevUsb/Stream
		!	DevStmSrc/OnOpen						DevUsb/Open

	% USB device was opened successfully
%	!	DevUsb/OnDevice							Debug/Fire
	!	DevUsb/OnDevice							DevUsbOpened/True

	% ION: Close stream to device
	$	DevStmCl Io.Resource {}
		!	DevStmSrc/OnOpen						DevStmCl/Resource

	% Close device before notification so it can be used immediately
	!	DevStmSrc/OnOpen							DevUsb/Close
	!	DevStmSrc/OnOpen							DevStmCl/Close

	%% USB Device information

	% ADTN: Copy possible queried information from open
	$	CopyOpenKeys Adt.Keys { Keys ( IdVendor IdProduct SerialNumber Manufacturer Product BlahBlah ) }
		!	DevUsb/OnDevice						CopyOpenKeys/Dictionary

	% MISCN: Create dictionary to receive device information
	$	CreateArriveDct Misc.Create { Id Adt.Dictionary }
		!	DevUsbOpened/OnTrue					CreateArriveDct/Fire
		!	CreateArriveDct/OnFire				CopyOpenKeys/Copy

	% ADTN: Store device path in announcement
	$	StoreOpenPath Adt.Store { Key Name }
		!	OnEnum/OnFire							StoreOpenPath/Value
		!	CopyOpenKeys/OnCopy					StoreOpenPath/Dictionary
		!	CopyOpenKeys/OnCopy					StoreOpenPath/Fire

	% Announce
	!	CopyOpenKeys/OnCopy						OnArrive/Fire

	% Process device
	!	OnEnum/OnFire								DevStmSrc/Open
	!	OnEnum/OnFire								DevUsbOpened/Fire

	%%%%%%%%%%
	% Monitor
	%%%%%%%%%%

	%% Monitor changes to the USB device state.

	% ION: USB device monitoring (GUID_DEVINTERFACE_USB_DEVICE)
	$	Notify Io.NotifyDevices { Class "{A5DCBF10-6530-11D2-901F-00C04FB951ED}" }
		!	Start/OnFire							Notify/Start
		!	Stop/OnFire								Notify/Stop
		!	Notify/OnFire							Debug/Fire
		!	Notify/OnFire							Debug/Fire

	% ADTN: Load unique name/path of device
	$	LoadNotifyName Adt.Load { Key Name }
		!	Notify/OnFire							LoadNotifyName/Dictionary
		!	Notify/OnFire							LoadNotifyName/Fire

	% MISCN: Demux the event type
	$	DemuxEvent Misc.Demux { Key Event Values ( Arrive Depart ) }
		!	Notify/OnFire							DemuxEvent/Dictionary
		!	Notify/OnFire							DemuxEvent/Fire

	%%	Arrive

	% Enumerate device arrival
	!	LoadNotifyName/OnFire					OnEnum/Value
	!	DemuxEvent/OnArrive						OnEnum/Fire

	%% Remove

	% Remove device by its name
	!	LoadNotifyName/OnFire					OnRemoveName/Value
	!	DemuxEvent/OnDepart						OnRemoveName/Fire

	% Always stop on shutdown
	!	Uninitialize/OnFire						Stop/Fire

