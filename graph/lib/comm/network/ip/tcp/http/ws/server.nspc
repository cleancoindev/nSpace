%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
%
%								Server.NSPC
%
%							Websocket server.
%
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

@

	% Context
	$	PortSrvr				Misc.Dist {}			% Server port
	$	Client				Misc.Dist {}			% Set active client
	$	Transmit				Misc.Dist {}			% Transmit stream to client

	% Actions
	$	Initialize			Misc.Dist {}			% Initialize
	$	Uninitialize		Misc.Dist {}			% Uninitialize
	
	% Notifications
	$	OnReceive			Misc.Dist {}			% Message received
	$	OnClient				Misc.Dist {}			% Message client state
	$	OnOpen				Misc.Dist {}			% Connection open
	$	OnClose				Misc.Dist {}			% Connection closed

	% DEBUG
	$	Debug Misc.Debug {}
		!	OnReceive/OnFire						Debug/Fire

	%%%%%%%%
	% Setup
	%%%%%%%%

	% Subgraph: TCP server
	#	Server										Lib/Comm/Network/Ip/Tcp/ServerClients/ {}
%		!	Server/OnConnect/Fire				Debug/Fire
		!	Server/OnRead/Fire					Debug/Fire

	% Server port required
	!	PortSrvr/OnFire							Server/PortSrvr/Fire
	!	PortSrvr/OnFire							Server/Start/Fire

	% Current using the socket dictionary to contain the state
	!	Server/OnRead/OnFire						OnClient/Fire
	!	Server/OnRead/OnFire						Server/Socket/Fire

	% Subgraph: I/O
	#	Io												Lib/Comm/Network/Ip/Tcp/Io/ {}
		!	Server/OnRead/OnFire					Io/Socket/Fire
		!	Server/OnRead/OnFire					Io/Read/Fire

	% Subgraph: Parse stream into response
	#	StmToResp									Lib/Comm/Network/Ip/Tcp/Http/StmRsp/ {}
		!	StmToResp/OnResponse/OnFire		OnReceive/Fire

	% Subgraph: Convert response dictionary into stream
	#	RespToStm									Lib/Comm/Network/Ip/Tcp/Http/ReqStm/ {}

	% Subgraph: Websocket formats
	#	Format										Lib/Comm/Network/Ip/Tcp/Http/Ws/Format/ {}

	%%%%%%%%%%%
	% Incoming
	%%%%%%%%%%%

	% MISCN: Demux the web socket state
	$	DemuxWs Misc.Demux { Key WebSocket Values ( true ) }
		!	OnClient/OnFire						DemuxWs/Dictionary
		!	Io/OnStream/OnFire					DemuxWs/Fire

	% Parse request, currently assumes entire request comes in together (FIX)
	!	DemuxWs/OnDefault							Debug/Fire
	!	DemuxWs/OnDefault							StmToResp/Reset/Fire
	!	DemuxWs/OnDefault							StmToResp/Stream/Fire
	!	DemuxWs/OnDefault							StmToResp/Parse/Fire

	% If I/O subgraph detects closer, tell server
	!	Io/OnClose/OnFire							Server/Socket/Fire
	!	Io/OnClose/OnFire							Server/Close/Fire

	%%%%%%%%%%
	% Upgrade
	%%%%%%%%%%

	% MISCN: Demux the upgrade key
	$	DemuxUp Misc.Demux { Key Upgrade Values ( WebSocket ) }
		!	OnReceive/OnFire						DemuxUp/Dictionary
		!	OnReceive/OnFire						DemuxUp/Fire

	% ADTN: Load the provided key
	$	LoadKey Adt.Load { Key WebSocketKey }
		!	DemuxUp/OnWebSocket					LoadKey/Dictionary
		!	DemuxUp/OnWebSocket					LoadKey/Fire

	%% Per RFC 6455 GUID is appended to key, SHA1 hash is generated, and the base64 string will be sent back.

	% MATHN: Append WebSocket GUID 
	$	AppendUpGUID Math.Binary { Right "258EAFA5-E914-47DA-95CA-C5AB0DC85B11" }
		!	LoadKey/OnFire							AppendUpGUID/Left
		!	LoadKey/OnFire							AppendUpGUID/Fire
		!	AppendUpGUID/OnFire					Debug/Fire

	%% Place string in stream for the SHA1 calculation

	% MISCN: Create stream to receive hash
	$	CreateHashStm Misc.Create { Id Io.StmMemory }
		!	AppendUpGUID/OnFire					CreateHashStm/Fire

	% MISCN: Reset position/size of stream as needed
	$	StmPos Io.StreamOp {}
		!	CreateHashStm/OnFire					StmPos/Stream

	% MISCN: String to hash into stream
	$	StrUp Misc.StringStream {}
		!	CreateHashStm/OnFire					StrUp/Stream
		!	AppendUpGUID/OnFire					StrUp/String
		!	AppendUpGUID/OnFire					StrUp/To

	% Reset position of stream for use
	!	StrUp/OnFire								StmPos/Seek

	% OPENSSL: Generate SHA1 hash
	$	HashUp OpenSSL.EVPSign { Type SHA1 }
		!	CreateHashStm/OnFire					HashUp/Stream
		!	HashUp/OnFinal							Debug/Fire

	% Add contents of stream to digest
	!	StrUp/OnFire								HashUp/Begin
	!	StrUp/OnFire								HashUp/Update

	% Re-use hash stream to receive result
	!	StrUp/OnFire								StmPos/Size
	!	StrUp/OnFire								HashUp/Final

	% Reset position of stream for use
	!	HashUp/OnFinal								StmPos/Seek

	%%%%%%%%%%%
	% Response
	%%%%%%%%%%%

	% MISCN: Response template
	$	ValueWsResp Misc.Dist { Value { Method	Response Response	Switch } }
		!	Initialize/OnFire						ValueWsResp/Fire

	% ADTN: Store acceptance key in header
	$	StoreWsAccept Adt.Store { Key WebSocketAccept }
		!	ValueWsResp/OnFire					StoreWsAccept/Dictionary
		!	HashUp/OnFinal							StoreWsAccept/Fire

	% Convert dictionary to stream
	!	StoreWsAccept/OnFire						Debug/Fire
	!	StoreWsAccept/OnFire						RespToStm/Dictionary/Fire
	!	StoreWsAccept/OnFire						RespToStm/ToStream/Fire

	% Send resposne stream to current socket
	!	RespToStm/OnStream/OnFire				Io/Stream/Fire
	!	RespToStm/OnStream/OnFire				Io/Transmit/Fire

	% ADTN: Now in websocket mode
	$	StoreWs Adt.Store { Key WebSocket Value true:bool }
		!	OnClient/OnFire						StoreWs/Dictionary
		!	RespToStm/OnStream/OnFire			StoreWs/Fire

	% Announce connection
	!	StoreWs/OnFire								OnOpen/Fire

	%%%%%%%%%%%%%
	% Data frame
	%%%%%%%%%%%%%

	%% Data received after websocket established.  Process frames.
	!	DemuxWs/Ontrue								Debug/Fire

	% MISCN: Create dictionary to received parsed frame
	$	CreateFrmDct Misc.Create { Id Adt.Dictionary }
		!	DemuxWs/Ontrue							CreateFrmDct/Fire

	% MISCN: Initial parse of header
	$	FrmPrs Misc.DictParse {}
		!	Format/ValueFrmIn/OnFire			FrmPrs/Format
		!	DemuxWs/Ontrue							FrmPrs/Stream
		!	CreateFrmDct/OnFire					FrmPrs/Dictionary
		!	FrmPrs/OnFire							Debug/Fire

	% MISCN: Demux the opcode for the message
	$	DemuxOp Misc.Demux { Key OpCode Values ( Text Binary Close ) }
		!	FrmPrs/OnFire							DemuxOp/Dictionary
		!	FrmPrs/OnFire							DemuxOp/Fire

	%%
	%% The minimum number of bytes are used to encode the payload length.  Read the proper
	%% amount of additional length bytes based on the initial payload length.
	%%

	% ADTN: Load payload length
	$	LoadFrmLenI Adt.Load { Key Length }
		!	DemuxOp/OnText							LoadFrmLenI/Dictionary
		!	DemuxOp/OnText							LoadFrmLenI/Fire
		!	DemuxOp/OnBinary						LoadFrmLenI/Dictionary
		!	DemuxOp/OnBinary						LoadFrmLenI/Fire

	% MISCN: Length check
	$	IsLengthT Misc.Compare { Left 126:int }
		!	LoadFrmLenI/OnFire					IsLengthT/Fire
	$	IsLengthE Misc.Compare { Left 127:int }
		!	IsLengthT/OnNotEqual					IsLengthE/Fire

	% MISCN: Read additional two or four byte lengths from stream
	$	FrmPrsLenT Misc.DictParse { Endian Big Format ( { Name Length Type Integer Size 2:int } ) }
		!	CreateFrmDct/OnFire					FrmPrsLenT/Dictionary
		!	DemuxWs/Ontrue							FrmPrsLenT/Stream
		!	IsLengthT/OnEqual						FrmPrsLenT/Fire
%		!	FrmPrsLenT/OnFire						Debug/Fire
	$	FrmPrsLenE Misc.DictParse { Endian Big Format ( { Name Length Type Long } ) }
		!	CreateFrmDct/OnFire					FrmPrsLenE/Dictionary
		!	DemuxWs/Ontrue							FrmPrsLenE/Stream
		!	IsLengthE/OnEqual						FrmPrsLenE/Fire
%		!	FrmPrsLenE/OnFire						Debug/Fire

	%%
	%% If mask is present, the 4 byte masking value must be extracted next.
	%%

	% MISCN: Demux the mask flag
	$	DemuxMask Misc.Demux { Key Mask Values ( true ) } 
		!	DemuxOp/OnText							DemuxMask/Dictionary
		!	DemuxOp/OnText							DemuxMask/Fire
		!	DemuxOp/OnBinary						DemuxMask/Dictionary
		!	DemuxOp/OnBinary						DemuxMask/Fire

	% MISCN: Extract masking value
	$	FrmPrsMask Misc.DictParse { Format ( { Name MaskValue Type Integer Size 4:int } ) }
		!	CreateFrmDct/OnFire					FrmPrsMask/Dictionary
		!	DemuxWs/Ontrue							FrmPrsMask/Stream
		!	DemuxMask/Ontrue						FrmPrsMask/Fire

	%%
	%% Create/fill a stand-alone stream that just has the client data in it for the
	%% exact amount of the payload.  This allows masking to take place and allows
	%% multiple requests in a single stream to be received and processed.
	%%

	% MISCN: Create stream to use for client payload
	$	CreateClientStm Misc.Create { Id Io.StmMemory }
		!	Initialize/OnFire						CreateClientStm/Fire

	% ADTN: Load the final computed length of the payload
	$	LoadLength Adt.Load { Key Length }
		!	DemuxMask/OnDefault					LoadLength/Dictionary
		!	DemuxMask/OnDefault					LoadLength/Fire
		!	FrmPrsMask/OnFire						LoadLength/Dictionary
		!	FrmPrsMask/OnFire						LoadLength/Fire

	% ION: Reset the client stream for use
	$	CliStmPos Io.StreamOp {}
		!	CreateClientStm/OnFire				CliStmPos/Stream
		!	LoadLength/OnFire						CliStmPos/Seek

	% ION: Pre-size the client stream
	$	CliStmSize Io.StreamOp {}
		!	CreateClientStm/OnFire				CliStmSize/Stream
		!	LoadLength/OnFire						CliStmSize/Offset
		!	LoadLength/OnFire						CliStmSize/Size

	% ION: Copy payload bytes from received stream to client stream
	$	CopyCliStm Io.StreamCopy {}
		!	CreateClientStm/OnFire				CopyCliStm/Destination
		!	DemuxWs/Ontrue							CopyCliStm/Source
		!	LoadLength/OnFire						CopyCliStm/Size
		!	LoadLength/OnFire						CopyCliStm/Fire
		!	CopyCliStm/OnFire						Debug/Fire

	% Reset the client stream for use
	!	CopyCliStm/OnFire							CliStmPos/Seek

	%% 
	%% The remaining data is payload data (possibly masked)
	%%
	!	FrmPrs/OnFire								Debug/Fire

	% ADTN: Check for mask value
	$	LoadMask Adt.Load { Key MaskValue }
		!	FrmPrsMask/OnFire						LoadMask/Dictionary
		!	FrmPrsMask/OnFire						LoadMask/Fire

	% MATHN: Apply mask to remaining bytes according to RFC
	$	MaskXor Math.Binary { Op Xor }
		!	CreateClientStm/OnFire				MaskXor/Left
		!	LoadMask/OnFire						MaskXor/Right

	% MISCN: Create stream to receive unmasked hash
	$	CreateMaskStm Misc.Create { Id Io.StmMemory }
		!	LoadMask/OnFire						CreateMaskStm/Fire
		!	CreateMaskStm/OnFire					MaskXor/Fire

	% MISCN: Reset position/size of stream as needed
	$	MaskStmPos Io.StreamOp {}
		!	MaskXor/OnFire							MaskStmPos/Seek

	% Debug
%	!	MaskXor/OnFire								Debug/Fire

	% MISCN: Resulting socket/stream
	$	ValueStmRx Misc.Dist {}
		!	CreateClientStm/OnFire				ValueStmRx/Value
		!	CreateMaskStm/OnFire					ValueStmRx/Value
		!	ValueStmRx/OnFire						OnReceive/Fire

	% Result
	!	DemuxOp/OnText								ValueStmRx/Fire
	!	DemuxOp/OnBinary							ValueStmRx/Fire

	% Latent connections, keep parsing stream until no more data
	!	DemuxWs/Ontrue								FrmPrs/Fire
	!	DemuxOp/OnText								FrmPrs/Fire
	!	DemuxOp/OnBinary							FrmPrs/Fire

	%%%%%%%%
	% Close
	%%%%%%%%

	% Client is closing connection.
	!	DemuxOp/OnClose							Debug/Fire
	!	DemuxOp/OnClose							Io/Close/Fire
	!	DemuxOp/OnClose							Server/Close/Fire

	% I/O subgraph stating socket should be closed
	!	Io/OnClose/OnFire							Server/Socket/Fire
	!	Io/OnClose/OnFire							Server/Close/Fire

	% Announce closures
	!	Server/OnClose/OnFire					Io/Close/Fire
	!	Server/OnClose/OnFire					OnClose/Fire

	%%%%%%%%%%%
	% Transmit
	%%%%%%%%%%%

	%% TODO: Asynchronous transmission per client ?

	% Active client
	!	Client/OnFire								Io/Socket/Fire

	% MISCN: Transmission template TODO: When payload size > 7 bits
%	$	ValueWsTx Misc.Dist { Value { OpCodeBits 0x81:int LengthSize 1:int } }
	$	ValueWsTx Misc.Dist { Value { OpCodeBits 0x82:int LengthSize 1:int } }
		!	Initialize/OnFire						ValueWsTx/Fire

	% MISCN: Size of outgoing stream
	$	StmPosTx Io.StreamOp {}
		!	Transmit/OnFire						StmPosTx/Available

	% MISCN: Determine required length size
	$	LenEightTx Misc.Compare { Left 0x10000:int }
		!	StmPosTx/OnAvailable					LenEightTx/Fire
	$	LenTwoTx Misc.Compare { Left 125:int }
		!	LenEightTx/OnGreater					LenTwoTx/Fire

	% MISCN: Length sizes
	$	ValueEightTx Misc.Dist { Value 8:int }
		!	LenEightTx/OnEqual					ValueEightTx/Fire
		!	LenEightTx/OnLess						ValueEightTx/Fire
	$	ValueTwoTx Misc.Dist { Value 2:int }
		!	LenTwoTx/OnLess						ValueTwoTx/Fire
	$	ValueOneTx Misc.Dist { Value 1:int }
		!	LenTwoTx/OnEqual						ValueOneTx/Fire
		!	LenTwoTx/OnGreater					ValueOneTx/Fire

	% ADTN: Store size and data in dictionary
	$	StoreWsTx Adt.Keys { Keys ( Length LengthSize Data ) }
		!	ValueWsTx/OnFire						StoreWsTx/Dictionary
		!	ValueOneTx/OnFire						StoreWsTx/LengthSize
		!	ValueTwoTx/OnFire						StoreWsTx/LengthSize
		!	ValueEightTx/OnFire					StoreWsTx/LengthSize
		!	StmPosTx/OnAvailable					StoreWsTx/Length
		!	Transmit/OnFire						StoreWsTx/Data
		!	Transmit/OnFire						StoreWsTx/Store
		!	StoreWsTx/OnStore						Debug/Fire

	% MISCN: Create stream for outgoing bytes
	$	CreateTxStm Misc.Create { Id Io.StmMemory }
		!	StoreWsTx/OnStore						CreateTxStm/Fire

	% MISCN: Initial parse of header
	$	FrmFmt Misc.DictFormat { Endian Big }
		!	Format/ValueFrmOut/OnFire			FrmFmt/Format
		!	CreateTxStm/OnFire					FrmFmt/Stream
		!	StoreWsTx/OnStore						FrmFmt/Dictionary
		!	StoreWsTx/OnStore						FrmFmt/Fire

	% Reset position of stream
	!	FrmFmt/OnFire								StmPosTx/Seek

	% Request transmission
	!	FrmFmt/OnFire								Io/Stream/Fire
	!	FrmFmt/OnFire								Io/Transmit/Fire

